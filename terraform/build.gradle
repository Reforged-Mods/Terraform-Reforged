plugins {
	id "com.github.johnrengelman.shadow" version "7.0.0"
}

loom{
	mixinConfigs = ["mixins.terraform-biome-builder.json","mixins.terraform-dirt.json","mixins.terraform-leaves.json","mixins.terraform-signs.json","mixins.terraform-overworld-biome-extensions.json","mixins.terraform-tree-api-v1.json"]
}


configurations {
	shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}
archivesBaseName = "terraform"
version = formatVersion(project, "1.0.1")

dependencies {
	def deps = [":terraform-biome-builder-api-v1", ":terraform-config-api-v1", ":terraform-dirt-api-v1", ":terraform-overworld-biome-extensions-api-v1", ":terraform-shapes-api-v1", ":terraform-surfaces-api-v1", ":terraform-tree-api-v1", ":terraform-utils-v1", ":terraform-wood-api-v1"]
	for (String s : deps){
		shadowCommon(project(path: s, configuration: "remapped"))
	}
	compileOnly(project(":terraform-wood-api-v1")){
		transitive = false
	}
	compileOnly(project(":terraform-dirt-api-v1")){
		transitive = false
	}
	compileOnly(project(":terraform-tree-api-v1")){
		transitive = false
	}
}

shadowJar {
	exclude "fabric.mod.json"
	from zipTree(remapJar.archiveFile)
	assemble.dependsOn shadowJar
	configurations = [project.configurations.shadowCommon]
	classifier ""
}

afterEvaluate{
	remapJar {
		classifier "thin"
	}

	jar {
		classifier "dev"
	}
}

publishing {
	publications {
		mavenJava(org.gradle.api.publish.maven.MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(shadowJar) {
				builtBy shadowJar
			}
		}
	}
}
