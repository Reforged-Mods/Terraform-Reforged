import org.gradle.api.tasks.bundling.Jar
import org.gradle.jvm.toolchain.JavaLanguageVersion

plugins {
	id 'java'
	id 'maven-publish'
	id 'dev.architectury.loom' version '1.4-SNAPSHOT' apply false
}

allprojects {
	apply plugin: 'maven-publish'
	apply plugin: 'java'
	apply plugin: 'dev.architectury.loom'


	java.toolchain.languageVersion = JavaLanguageVersion.of(17)

	repositories {
		mavenLocal()
	}

	dependencies {
		minecraft "com.mojang:minecraft:$project.minecraft_version"
		mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"

		forge "net.minecraftforge:forge:$project.minecraft_version-$project.forge_version"
	}

	processResources {
		inputs.property "version", project.version

		filesMatching("META-INF/mods.toml") {
			expand "version": project.version
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		archiveClassifier.set "sources"
		from sourceSets.main.allSource
	}

	tasks.withType(JavaCompile) {
		it.options.encoding = "UTF-8"
	}
}

subprojects {


	configurations { remapped }
	artifacts.add("remapped", remapJar)
	version = rootProject.version
	group = rootProject.maven_group

	publishing {
		publications {
			mavenJava(MavenPublication) {
				// add all the jars that should be included when publishing to maven
				artifact(remapJar) {
					builtBy remapJar
				}
				artifact(sourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}
		setupRepositories(repositories)
	}
}

void setupRepositories(RepositoryHandler repositories) {
	//repositories.mavenLocal() // uncomment for testing
	def ENV = System.getenv()
	repositories.maven {
		url System.getenv("local_maven_url")
	}
}

archivesBaseName = "terraform-forge"
version = "${version}"

dependencies {
	subprojects.each {
		if (it.name != "terraform-biome-remapper-api-v1"){
			implementation project(path: "${it.name}", configuration: "namedElements")
		}
	}
	afterEvaluate {
		subprojects.each {
			if (it.name != "terraform-biome-remapper-api-v1"){
				include project("${it.name}:")
			}
		}
	}
}
